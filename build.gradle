buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
    }
}


apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
//apply plugin: 'eclipse'
apply plugin: 'com.github.johnrengelman.shadow'


group = 'net.buycraft' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'buycraftx-forge-1.18.2'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: '1.18.2'

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            property 'forge.enabledGameTestNamespaces', 'Tebex-Forge'

            mods {
                buycraft {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', 'Tebex-Forge'

            mods {
                buycraft {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    mavenLocal()
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.2-40.1.80'
    shadow('net.buycraft:buycraftx-plugin-shared:12.0.8')
    minecraftLibrary 'net.buycraft:buycraftx-plugin-shared:12.0.8'
    minecraftLibrary 'net.buycraft:buycraftx-common:12.0.8'

}

version = configurations.getByName("shadow").dependencies.find().version // copy common version

shadowJar {
    configurations = [project.configurations.shadow]

    relocate 'okhttp3', 'net.buycraft.plugin.internal.okhttp3'
    relocate 'okio', 'net.buycraft.plugin.internal.okio'
    relocate 'retrofit2', 'net.buycraft.plugin.internal.retrofit2'
    relocate 'com.fasterxml.jackson', 'net.buycraft.plugin.internal.jackson'

    minimize {
        exclude("com/google/**")
        exclude("javax/annotation/**")
        exclude("org/**")

        exclude("META-INF/proguard/*")
        exclude("META-INF/retrofit.*")
    }

    classifier ''
}

afterEvaluate {
    reobf {
        shadowJar {
            mappings = project.tasks.getByName('createMcpToSrg').output
        } // Reobfuscate the shadowed JAR
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "BuycraftX",
                "Specification-Vendor"    : "Tebex",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "Tebex",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

assemble.dependsOn shadowJar

reobf {
    shadowJar {}
}